plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version "4.6.0"
    id "org.sonarqube" version "3.1.1"
}

group 'org.example'
version '1.0-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
    compile 'org.springdoc:springdoc-openapi-ui:1.5.3'
    testCompile 'org.springdoc:springdoc-openapi-webmvc-core:1.5.3'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.0.RELEASE'

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

checkstyle {
    toolVersion = 8.39
    ignoreFailures = false
    showViolations = true

}

pmd {
    ignoreFailures = false
    toolVersion = "6.31.0"
}
pmdTest{
    enabled = false
}
spotbugs{
    ignoreFailures = false
    showStackTraces = true
    showProgress = true
    effort = 'max'
    visitors = [ 'FindSqlInjection', 'SwitchFallthrough' ]
    omitVisitors = [ 'FindNonShortCircuit' ]
}

tasks['sonarqube'].dependsOn test
